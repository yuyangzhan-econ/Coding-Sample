/******************************************************************************************* 
* NAME:1_Tables.do
* Date: 2025/7/10
* CREATED by ZHAN Yuyang
* NOTES:
  This document uses the datasets generated by `0_Data_Preparation.do`—namely `$dir/data/firm_level_outcomes.dta` and `$dir/data/firm_to_firm_level_outcomes.dta`—to perform regression analysis. 
  The regression results are stored in:
- `$dir/results/firm_level_results.csv`
- `$dir/results/firm_to_firm_level_results.csv`

Directory：
- PRELIMINARIES
- 1. Firm-level regressions (Pre-Post Analysis)
- 2. Firm-to-firm level regressions (Pre-Post Analysis)
*******************************************************************************************/


*****************
/*PRELIMINARIES*/
*****************
clear all
set maxvar 15000
set matsize 10000

*If you want to run this dofile separately, rather than through master.do, fill in your path here.

*    global dir "XXXXX"

	
*set seed
set seed 123456789


*************************************************
/*1. Firm-level regressions (Pre-Post Analysis)*/
*************************************************
use "$dir/data/firm_level_outcomes.dta", clear

* Set up
local outcomes "outcome_value n_partners new_partners n_products" //Define outcome variables: Total sales and purchases. Total of buyers and suppliers. Number of new buyers and suppliers. Number of products purchased and sold.
local counter = 1
gen treat_post = treat * post

* Regression analysis of suppliers
preserve
keep if type == "supplier"
foreach outcome in `outcomes' {
	di `outcomes'
    reghdfe `outcome' treat_post, absorb(firm_id months_to_fair strata_id#months_to_fair) vce(cluster firm_id)
    eststo supplier_`counter'
    local counter = `counter' + 1
}

* Export firm-level results (suppliers)
esttab supplier_* using "$dir/results/tables/firm_level_results.csv", replace ///
    b(3) se(3) ///
    star(* 0.10 ** 0.05 *** 0.01) ///
    stats(N r2, fmt(0 3) labels("Observations" "R-squared")) ///
    mtitles("Total Sales" "Total Buyers" "New Buyers" "Number of Products") ///
    title("Firm-Level Treatment Effects (suppliers)") ///
    nonotes addnotes("Standard errors in parentheses, clustered at firm level." "*** p<0.01, ** p<0.05, * p<0.1" "" "") 
restore
	
* Regression analysis of buyers
preserve
keep if type == "buyer"
foreach outcome in `outcomes' {
	di `outcomes'
    reghdfe `outcome' treat_post, absorb(firm_id months_to_fair strata_id#months_to_fair) vce(cluster firm_id)
    eststo buyer_`counter'
    local counter = `counter' + 1
}

* Export firm-level results (buyers)
esttab buyer_* using "$dir/results/tables/firm_level_results.csv", append ///
    b(3) se(3) ///
    star(* 0.10 ** 0.05 *** 0.01) ///
    stats(N r2, fmt(0 3) labels("Observations" "R-squared")) ///
    mtitles("Total Purchases" "Total Suppliers" "New Suppliers" "Number of Products") ///
    title("Firm-Level Treatment Effects (buyers)") ///
    nonotes addnotes("Standard errors in parentheses, clustered at firm level." "*** p<0.01, ** p<0.05, * p<0.1")
restore


*********************************************************
/*2. Firm-to-firm level regressions (Pre-Post Analysis)*/
*********************************************************
use "$dir/data/firm_to_firm_level_outcomes.dta", clear

* Set up
local outcomes "has_sale n_transactions n_products" //Define outcome variables: Whether there is a sale (or purchase). Number of transactions. Total sales amount.
local counter = 1
gen treat_post = treat * post

* Regression analysis of firm pairs
foreach outcome in `outcomes' {
	di `outcomes'
    reghdfe `outcome' treat_post, absorb(pair_id months_to_fair strata_id#months_to_fair) vce(cluster seller)
    eststo pair_`counter'
    local counter = `counter' + 1
}

* Export firm-level results (firm pairs)
esttab pair_* using "$dir/results/tables/firm_to_firm_level_results.csv", replace ///
    b(3) se(3) ///
    star(* 0.10 ** 0.05 *** 0.01) ///
    stats(N r2, fmt(0 3) labels("Observations" "R-squared")) ///
    mtitles("Whether there is a sale" "Number of transactions" "Total sales amount") ///
    title("Firm-to-firm-Level Treatment Effects") ///
    nonotes addnotes("Standard errors in parentheses, clustered at firm level." "*** p<0.01, ** p<0.05, * p<0.1" "" "") 


di "Analysis complete."

eststo clear